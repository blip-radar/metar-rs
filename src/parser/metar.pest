metar = {
    SOI ~
    station ~
    observation_time ~
    corrected? ~
    auto? ~
    (wind ~ wind_varying?)? ~
    atmos_condition? ~
    temperatures? ~
    pressure? ~
    recents? ~
    windshear* ~
    runway_state* ~
    cloud_direction* ~
    (colour_code ~ colour_code?)? ~
    trend* ~
    remarks? ~
    "="? ~
    EOI
}

station = @{
    ASCII_ALPHA_UPPER{4}
}

observation_time = ${
    observation_day ~
    observation_hour ~
    observation_minute ~
    "Z"
}

observation_day = @{
    (( '0'..'2' ) ~ ASCII_DIGIT) |
    ("3" ~ ( "0" | "1" ))
}

observation_hour = @{
    (( "0" | "1" ) ~ ASCII_DIGIT) |
    ("2" ~ ( '0'..'3' ))
}

observation_minute = @{
    ('0'..'5') ~ ASCII_DIGIT
}

corrected = @{ "COR" }
auto = @{ "AUTO" }

wind = ${
    (wind_dir ~
    wind_speed ~
    wind_gusts? ~ wind_unit)
}

wind_dir = @{
    "///" |
    "VRB" |
    (('0'..'2') ~ ASCII_DIGIT{2}) |
    ("3" ~ ('0'..'6') ~ ASCII_DIGIT)
}

wind_speed = @{
    "//" |
    ("P"? ~ ASCII_DIGIT{2,3})
}

wind_gusts = @{
    "G" ~ ASCII_DIGIT{2}
}

wind_unit = @{
    "KT" | "MPS" | "KPH"
}

wind_varying = ${
    wind_dir ~ "V" ~ wind_dir
}

atmos_condition = {
    "CAVOK" |
    "SKC" |
    ((visibility_horizontal)? ~
     restricted_visibility_horizontal* ~
     rvr* ~
     ("SKC" | "CLR" |
      wx* ~
      visibility_vertical? ~
      (no_clouds_detected | "NSC" | "CLR" | cloud*)))
}

visibility_horizontal = @{
    "////" |
    (ASCII_DIGIT{4}) |
    (ASCII_DIGIT{1,2} ~ "SM") |
    (ASCII_DIGIT ~ " " ~ ASCII_DIGIT ~ "/" ~ ASCII_DIGIT ~ "SM") |
    (ASCII_DIGIT ~ "/" ~ ASCII_DIGIT ~ "SM")
}

compass_direction = _{
    "NE" | "NW" | "N" | "SE" | "SW" | "S" | "E" | "W"
}

restricted_visibility_horizontal = ${
    visibility_horizontal ~ compass_direction
}

visibility_vertical = @{
    "VV" ~ ("///" | ASCII_DIGIT{3})
}

rvr = ${
    "R" ~ runway_number ~ "/" ~ (rvr_visibility_range | rvr_visibility) ~ "FT"? ~ rvr_trend?
}

runway_number = @{
    (('0'..'2') ~ ASCII_DIGIT ~ runway_suffix?) |
    ("3" ~ ('0'..'6') ~ runway_suffix?)
}

runway_suffix = @{
    "L" | "C" | "R"
}

rvr_visibility_range = ${
    rvr_visibility ~ "V" ~ rvr_visibility
}

rvr_visibility = @{
    ("P" | "M")? ~
    ASCII_DIGIT{4}
}

rvr_trend = @{
    "D" | "U" | "N"
}

wx = ${
    ("//" | (wx_intensity? ~ wx_condition+)) ~ (WHITESPACE | EOI)
}

wx_intensity = @{
    "+" | "-" | "VC"
}

wx_condition = @{
    "MI" | "PR" | "BC" | "DR" | "BL" | "SH" | "TS" | "FZ" |
    "RA" | "DZ" | "SN" | "SG" | "IC" | "PL" | "GR" | "GS" |
    "UP" | "FG" | "VA" | "BR" | "HZ" | "DU" | "FU" | "SA" |
    "PY" | "SQ" | "PO" | "DS" | "SS" | "FC"
}

no_clouds_detected = @{ "NCD" }
cloud = ${
    cloud_density ~ cloud_floor ~ cloud_type?
}

cloud_density = @{
    "///" | "FEW" | "SCT" | "BKN" | "OVC"
}

cloud_floor = @{
    "///" |
    ASCII_DIGIT{3}
}

cloud_type = @{
    "///" | "CB" | "TCU"
}

temperatures = ${
    temperature ~ "/" ~ temperature
}

temperature = @{
    "//" | ("M"? ~ ASCII_DIGIT{2})
}

pressure = @{
    ("Q" | "A") ~
    (ASCII_DIGIT{4} | "////")
}

recents = ${
    "RE" ~ wx_condition+
}

windshear = {
    "WS ALL RWY" |
    "WS" ~ windshear_group+
}

windshear_group = @{
    ("TKOF RWY " ~ runway_number) |
    // negative lookahead for runway_state
    (!("R" ~ (runway_number | "88") ~ "/" ~ ASCII_ALPHANUMERIC+) ~
    "R" ~ runway_number)
}

runway_state = ${
    "R" ~ (runway_number | "88") ~ "/" ~
    ("CLRD" | (runway_deposits ~ runway_contamination ~ runway_deposit_depth)) ~
    runway_braking_action
}

runway_deposits = @{
    "/" | '0'..'9'
}

runway_contamination = @{
    "1" | "2" | "5" | "9" | "/"
}

runway_deposit_depth = @{
    ASCII_DIGIT{2} | "//"
}

runway_braking_action = @{
    (('1'..'8') ~ ASCII_DIGIT) |
    ("9" ~ ('1'..'5' | "9")) |
    "//"
}

trend = {
    no_significant_change |
    becoming |
    tempo
}

no_significant_change = @{
    "NOSIG"
}
becoming = {
    "BECMG" ~ wx_change_time? ~ wx_change_conditions
}
tempo = {
    "TEMPO" ~ wx_change_time? ~ wx_change_conditions
}

no_significant_weather = @{
    "NSW"
}

wx_change_time = @{
    ("FM" | "TL" | "AT") ~ ASCII_DIGIT{4}
}

wx_change_conditions = {
    no_significant_weather? ~ (wind ~ wind_varying?)? ~ colour_code? ~ atmos_condition
}

cloud_direction = ${
    cloud_type ~ ("/" ~ compass_direction)+
}

colour_code = {
   colour_code_blue_plus | colour_code_blue | colour_code_white | colour_code_green
   | colour_code_yellow | colour_code_amber | colour_code_red | colour_code_unknown
}
colour_code_blue_plus = @{ "BLU+" }
colour_code_blue = @{ "BLU" }
colour_code_white = @{ "WHT" }
colour_code_green = @{ "GRN" }
colour_code_yellow = @{ "YLO" }
colour_code_amber = @{ "AMB" }
colour_code_red = @{ "RED" }
colour_code_unknown = @{ "///" }

remarks = {
    "RMK" ~
    (ASCII_ALPHANUMERIC | "$" | "/" | "-")*
}

WHITESPACE = _{ " " }
